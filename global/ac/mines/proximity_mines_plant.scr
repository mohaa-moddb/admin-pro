// //made by Elgan Sayer (sfx)

main:
	level.gonecount=1
	level.count=1

	local.master = spawn ScriptMaster
	local.master aliascache plantbomb1 sound/items/Item_BangaloreAssemble_01.wav soundparms 1.0 0.0 1.0 0.0 100 2000 item loaded maps "m dm moh obj train"


	while (level.run["mines"] == "1")
	{
		 for(level.i = 1; level.i <= $player.size; level.i++)
		 {
			if ($player[level.i].hastrigger!=1 && $player[level.i] != NIL)
			{
				if(level.run["mines"] != "1")
				{
					end
				}

				local.bomb = spawn trigger_multiple 
				local.bomb.origin = $player[level.i].origin
				local.bomb setthread bomb_place
				local.bomb setsize ( -20 -20 -20 ) ( 20 20 20 )	
				local.bomb glue $player[level.i]
				$player[level.i].hastrigger=1
				$player[level.i].limit=0
				thread death $player[level.i]
			}

		}
	waitframe
	}

end

bomb_place:

	if(level.run["mines"] != "1")
	{
		end
	}

	local.player = parm.other

	local.set = 0
	
	local.guy = local.player
	if(local.player.stickybomb==1)
	{	
		end
	}

	level.limit = waitexec global/settings.scr::getcmd "mines_ammo"

	level.limit = int level.limit		

	while(local.player.useheld == 1)
	{
		if (local.player.ARMED==1 || local.player.WALKIETALKIED==1){end}

		if ( local.player.ARMED==1 || local.player.DETONATORED==1){end}

		local.set++
		if(local.set ==2)
		{
			local.set = 0

			if(local.player.stickybomb==1 || local.player.flying==1)
			{	
				end
			}

			if(local.player.limit >= level.limit)
			{
				local.player iprint "Out of bombs" 1
				end
			}

			local.player.limit++
			level.count++

			local.count = level.count
			local.player.stickybomb=1
	
			
			local.fwd_vec = angles_toforward local.player.viewangles
			local.start = local.player gettagposition "Bip01 R Hand"

			local.hit_location =  local.start - local.fwd_vec 

			local.player iprint ("Proximity Mine: " + local.player.limit)

			local.dest = trace (local.start + local.fwd_vec * 64) (local.start + local.fwd_vec * 150) 0
			//local.player aimat local.dest
			local.vect = (local.dest - local.player.origin)
			local.vect = vector_normalize (local.vect)

			local.dist = vector_length (local.player.origin - local.dest)
			
			local.vect[0] = local.vect[0] * (local.dist / 1.5)
			local.vect[1] = local.vect[1] * (local.dist / 1.5)
			local.vect[2] = local.vect[2] * local.dist

			local.vect[0] = local.vect[0] * 3
			local.vect[1] = local.vect[1] * 3

			local.vect[2] = local.vect[2] + 300

			local.mine = spawn script_model "targetname" ("mine" + local.count)
			local.mine model "items/explosive.tik"
			local.mine.origin = local.hit_location 
			local.mine physics_on
			local.mine gravity 1
			local.mine.angles = local.player.angles
			local.mine notsolid
			local.mine.team = local.player.dmteam
			local.mine.count = local.count

			level.killer[local.count] = local.player

			local.mine_trig = spawn trigger_multiple "spawnflags" "128"  "targetname" ("minetrig_shot" + local.count)
			local.mine_trig.origin = local.mine.origin
			local.mine_trig setsize ( -30 -30 -30 ) ( 30 30 30 ) 		
			local.mine_trig setthread triggerd
			local.mine_trig glue local.mine
			local.mine_trig.count = local.count

			local.mine_trig2 = spawn trigger_multiple  "targetname" ("minetrig_walk" + local.count)
			local.mine_trig2.origin = local.mine.origin
			local.mine_trig2 setsize ( -30 -30 -30 ) ( 30 30 30 ) 		
			local.mine_trig2 setthread triggerd_walk
			local.mine_trig2 glue local.mine
			local.mine_trig2.count = local.count

			local.mine_trig2 nottriggerable

			local.mine.origin = local.player.origin

			local.player stopwatch 5
			wait 5

			if!(local.mine_trig2)
			{
				end
			}
	
			local.mine_trig2 triggerable
			local.mine solid
			local.player iprint "Mine Activated"

			local.hide = waitexec global/settings.scr::getcmd "hide_mines"
			
			if (local.hide == "1")
			{
				local.mine hide
			}

			local.player playsound plantbomb1


			local.player.stickybomb=0
			end
		}
	wait 1
	}

end

death local.player:
	
	while(1)
	{
		while(isalive local.player)
		{
			waitframe
		}


		if(local.player==NULL)
		{
			end
		}

		local.player.stickybomb=0
		local.player.limit=0

		wait 1
	}
end



explode local.bomber local.explosionplace local.mine local.anyway:
	
	local.radius = 300.00 * 300.00

	for(local.i = 1; local.i <= $player.size; local.i++)
	{
		local.player= $player[local.i]
		local.distance = local.player.origin - local.explosionplace

		local.distance = local.distance * local.distance

		if(local.distance <= local.radius)
		{
			local.gametype = getcvar "g_gametype"

			local.gametype = int local.gametype
		
			if(local.player.dmteam!=local.bomber.dmteam)
			{
				local.dmg_fact = 1 - local.distance / local.radius
				local.dmg = 150 * local.dmg_fact + 30
				local.player damage local.bomber local.dmg local.mine (0 0 0) (0 0 0) (0 0 0) 0 1 rocket -1
				local.del=1
			}
			else if(local.gametype==1 || local.player ==  local.bomber)	
			{
				local.dmg_fact = 1 - local.distance / local.radius
				local.dmg = 150 * local.dmg_fact + 30
				local.player damage local.bomber local.dmg local.mine (0 0 0) (0 0 0) (0 0 0) 0 1 rocket -1
				local.del=1
			}

		}

	}

	if(local.del==1 || local.anyway == 1)
	{
		thread bang local.mine	
		$("minetrig_shot" + local.mine.count) remove
		$("minetrig_walk" + local.mine.count) remove	
		local.mine delete
	}

end



triggerd_walk:

	for(local.i=1; local.i <= level.count; local.i++)
	{

		local.mine = $("mine"+local.i)

		if(local.mine)
		{

			if(self istouching local.mine)
			{
				local.gametype = getcvar "g_gametype"

				local.gametype = int local.gametype

				if(local.gametype  > 1)
				{
					if(parm.other.dmteam == local.mine.team)
					{
						thread lightup local.mine
						end
					}
				}


				if(local.mine)
				{
					local.player = level.killer[local.i]
					thread explode local.player local.mine.origin local.mine
					end
				}

			}
		}
	}
end

triggerd:

	for(local.i=1; local.i <= level.count; local.i++)
	{
		local.mine = $("mine"+local.i)
		if(local.mine)
		{
			if(self istouching local.mine)
			{
				local.player=parm.other
				if(local.mine)
				{	
					thread explode local.player local.mine.origin local.mine 1
					end
				}
			}
		}
	}
end

bang local.mine:
	local.exp1 = spawn "models/emitters/explosion_mine.tik" 
	local.exp1.origin = local.mine.origin 
	local.exp1 scale 3

	local.exp = spawn "models/emitters/explosion_mine_shockwave.tik" 
	local.exp.origin = local.mine.origin 
	local.exp scale 3
	local.exp1 anim start
	local.exp anim start
	wait 1
	local.exp delete
	local.exp1 delete
 
end

lightup local.mine:

	if(local.mine.team=="axis")
	{
		local.mine light 1 0 0 50
	}
	else
	{
		local.mine light 0 0 1 50
	}

	wait 3
	if(local.mine )
	{
		local.mine light 0 0 0 0
	}
end
