
// print the adverts
main local.msg local.delay:

	if(level.msginginuse==1)
	{
		while(level.msginginuse==1)
		{
			wait 1
		}
	}

	local.delay = int local.delay

	level.msginginuse=1

	local.lightcolour = waitexec global/settings.scr::getcmd  "advert_colour" 
	local.font = waitexec global/settings.scr::getcmd  "advert_font" 
	local.scroll_speed = waitexec global/settings.scr::getcmd  "scroll_speed" 
	local.scroll_dir = waitexec global/settings.scr::getcmd "scroll_dir"
	local.wordcount = 0

	if(local.font == NIL)
	{
		local.font = "facfont-20"
	}

	if(local.lightcolour == NIL)
	{
		local.lightcolour = "0 1 0"
	}

	if(local.delay == NIL)
	{
		local.delay = 2
	}

	if(local.scroll_speed == NIL)
	{
		local.scroll_speed = 2
	}
	else
	{
		local.scroll_speed = int local.scroll_speed
	}

	if(local.scroll_dir == NIL)
	{
		local.scroll_dir = "left"
	}

	for(local.i=0;local.i<=local.lightcolour.size - 1;local.i++)
	{	

		if(local.lightcolour[local.i]!=" ")
		{
			if(local.light[local.wordcount]==NIL)
			{
				local.light[local.wordcount]=""
			}

			local.light[local.wordcount] += local.lightcolour[local.i]
		}
		else
		{	
			local.wordcount++
		}
	}

	if(local.scroll_dir == "left")
	{
		local.lengh = 300
		local.to = ( -300 -  ( local.msg.size * 10 ) )
	}
	else
	{
		local.lengh = ( -300 -  ( local.msg.size * 10 ) )
		local.to = ( 300 +  ( local.msg.size * 10 ) )
	}

	local.ended  = 1
	while(local.ended == 1)
	{
		// draw the overlay coloured string
		huddraw_virtualsize 51 1
		huddraw_rect 51 local.lengh 200 0 0
		huddraw_color 51 local.light[0] local.light[1] local.light[2]
		huddraw_font 51 local.font
		huddraw_string 51 local.msg 
		huddraw_align 51 "center" "center"
		huddraw_alpha 51 local.i


		if(local.scroll_dir == "left")
		{
			local.lengh -= local.scroll_speed
			local.ended = (local.to < local.lengh)
		}
		else
		{
			local.lengh += local.scroll_speed
			local.ended = (local.to > local.lengh)
		}

	
		waitframe
	}
   
	level.msginginuse=0
end 